buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.unclezs:sass-gradle-plugin:1.0.7'
    }
}

plugins {
    id "org.openjfx.javafxplugin" version "0.0.9" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
}

ext {
    jfxProject = ['app', 'app-framework']
    jfxProject = subprojects.findAll { jfxProject.contains(it.name) }
}

allprojects {
    group = "com.unclezs.novel.app.jfx"
    version = property("app.version")
    description = "Uncle Novel Project"

    repositories {
        mavenLocal()
        mavenCentral()
    }

}


subprojects { project ->
//  apply plugin: "checkstyle"

    apply plugin: "java"
    apply from: "${rootDir}/gradle/publications.gradle"
    apply from: "${rootDir}/gradle/dependency-management.gradle"

    sourceSets {
        main.output.resourcesDir = main.java.outputDir
    }

    tasks.withType(JavaCompile) {
        options.fork = true
        options.encoding = "UTF-8"
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        // 模块化编译参数
        File vmArgs = file("$projectDir/${project.name}.modularity")
        if (vmArgs.exists()) {
            vmArgs.readLines().each {
                if (!it.isBlank() && !it.startsWith("//")) {
                    options.compilerArgs.add(it)
                }
            }
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    test {
        useJUnitPlatform()
        include(["**/*Tests.class", "**/*Test.class"])
    }

//  checkstyle {
//    toolVersion = "8.41"
//    configDirectory.set(rootProject.file("src/checkstyle"))
//  }
}

configure(jfxProject) { project ->
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "com.unclezs.gradle.sass"

    javafx {
        version = "16"
        modules = ["javafx.base", "javafx.controls", "javafx.fxml", "javafx.graphics", "javafx.media", "javafx.swing", "javafx.web"]
    }
    jar {
        exclude "**/scss/**"
    }
}
